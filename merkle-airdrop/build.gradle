version = '1.0.0'

// for integration tests
sourceSets {
    intTest {}
}
configurations {
    intTestImplementation.extendsFrom testImplementation
    intTestRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
    compileOnly 'foundation.icon:javaee-api:0.9.2'
    implementation 'foundation.icon:javaee-scorex:0.5.4'
    implementation 'io.havah:havah-tokens:1.0.1'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'

    intTestImplementation project(':hsp20-token')
    intTestImplementation 'foundation.icon:javaee-integration-test:0.9.0'
    intTestImplementation 'foundation.icon:icon-sdk:2.4.0'
}

optimizedJar {
    mainClassName = 'io.havah.contract.MerkleAirdrop'
    archivesBaseName = 'merkle-airdrop'
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

deployJar {
    endpoints {
        deneb {
            uri = 'https://ctz.dev.havah.io/api/v3'
            nid = 0x110
        }
        vega {
            uri = 'https://ctz.vega.havah.io/api/v3'
            nid = 0x101
        }
    }
    keystore = rootProject.hasProperty('keystoreName') ? "$keystoreName" : ''
    password = rootProject.hasProperty('keystorePass') ? "$keystorePass" : ''
    parameters {
        arg('_name', 'test1')
        arg('_number', '1')
    }
}

test {
    useJUnitPlatform()
}

def sampleTokenJar = project(':hsp20-token').getTasks().getByName('optimizedJar')

task integrationTest(type: Test) {
    useJUnitPlatform()
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.intTest.output.classesDirs
    classpath = sourceSets.intTest.runtimeClasspath
    testLogging.showStandardStreams = true

    // use the common config files
    systemProperty('env.props', new File(project(':testinteg').projectDir, 'conf/env.props'))

    def prefix = 'score.path.'
    print(project.name)
    systemProperty(prefix + project.name, optimizedJar.outputJarName)
    systemProperty(prefix + 'sample-token', sampleTokenJar.outputJarName)
}

integrationTest.dependsOn(sampleTokenJar)

repositories {
    mavenCentral()
}